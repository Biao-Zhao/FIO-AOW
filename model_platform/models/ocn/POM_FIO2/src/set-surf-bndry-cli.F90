 SUBROUTINE SET_SURF_BNDRY_CLI
 USE NcFileMod
 USE DATA_KIND_MOD
 USE CONTROL 
 USE CONST, ONLY: RHOREF
 USE ALL_VAR, ONLY : FSM,DUM,DVM,WUSURF,WVSURF,STRX,STRY,HEAT,DQDT,TEMP,TSURF,WTSURF,T
 IMPLICIT NONE

! LOCAL 
 CHARACTER(LEN=200) FILENAME
 REAL(kind_r4) :: RDAY,W1,W3,QC,DQT
 INTEGER L1,L2
!PSA SEA LEVEL PRESSURE; TSA SEA SURFACE TEMPERATURE
!SW NET DOWNWARD SHORT-WAVE RADIATION
!SPD_D WIND SPEED; QAR MIXING RATIO
!CLD CLOUD COVER; NETHEAT_MM5 NET HEAT FLUX FROM MM5

!>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
 IF(DAY>=15)THEN
   L1=MONTH+1
 ELSE
   L1=MONTH
 END IF
 IF(L1==13)L1=1
 L2=L1-1
 IF(L2==0)L2=12
 RDAY=((DAY*24+HOUR)*3600+SEC)/86400.
 W1=(RDAY+MON(L2)/2.)/((MON(L1)+MON(L2))/2.)         &
     -int((RDAY+MON(L2)/2.)/((MON(L1)+MON(L2))/2.))
 W3=1.-W1

WRITE(6,*),"SCALE=",w1,w3,L1,L2
!>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
! SET WIND STRESS  
 DO J=ISLAT,IELAT
 DO I=ISLON,IELON
 WUSURF(I,J)=-(STRX(I,J,L1)*W1+STRX(I,J,L2)*W3)/RHOREF*DUM(I,J)*FACT
 WVSURF(i,j)=-(STRY(I,J,L1)*W1+STRY(I,J,L2)*W3)/RHOREF*DVM(I,J)*FACT
 END DO
 END DO
! SET HEAT FLUX
 DO J=ISLAT,IELAT
 DO I=ISLON,IELON
    QC=(HEAT(I,J,L1)*W1+HEAT(I,J,L2)*W3)*FSM(I,J)
    TSURF(I,J)=(TEMP(I,J,L1)*W1+TEMP(I,J,L2)*W3)*FSM(I,J)
    DQT=(DQDT(I,J,L1)*W1+DQDT(I,J,L2)*W3)*FSM(I,J)
    WTSURF(I,J)=((QC-DQT*(TSURF(I,J)-T(I,J,1)))/(-1*4.1876E+6))*FACT
 END DO
 END DO
 RETURN
 END
